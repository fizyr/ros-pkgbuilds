# Maintainer: Maarten de Vries

pkgname=aprt
pkgdesc='Arch Linux binary packare repository tools'
url='https://github.com/delftrobotics/python-aprt'
pkgver=0.1.7
pkgrel=1
arch=(any)
license=(BSD)

provides=(python-aprt)

depends=(python python-libarchive-c)
makedepends=(python-setuptools)

source=("python-aprt-$pkgver.tar.gz::https://github.com/delftrobotics/python-aprt/archive/$pkgver.tar.gz")
sha512sums=('9b9a61331244a0932b318d156415595c1e6cef13a846c01cd106580a3eb5b4c6c4bf58608ebe98a7352dcdaba84e12e17a604a827d2c1a5b8cc77f507c057849')

build() {
	cd "$srcdir/python-aprt-$pkgver"
	python ./setup.py build
}

package() {
	cd "$srcdir/python-aprt-$pkgver"
	python ./setup.py install --root "$pkgdir"
	install -Dt "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
